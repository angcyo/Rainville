import com.android.build.gradle.tasks.PackageApplication

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.github.amlcurran.showcaseview:library:5.0.0'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'org.apache.directory.studio:org.apache.commons.io:2.4'
    compile project(':libs:common')
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate.toInteger()
}

android {
    compileSdkVersion 21
    buildToolsVersion '19.1.0'

    defaultConfig {
        targetSdkVersion 19
        minSdkVersion 14
        versionCode getDate()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        debug {
            storeFile file(System.getProperty("user.home") + "/.android/debug.keystore")
        }

        release {
            // Save password to ~/.gradle/gradle.properties
            storeFile file(System.getProperty("user.home") + MC_ANDROID_STORE_PATH)
            storePassword MC_ANDROID_STORE_PASSWORD
            keyAlias "RainNoise"
            keyPassword MC_ANDROID_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
        }

        release {
            debuggable false
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile =
                            new File(output.outputFile.parent,
                                    output.outputFile.name.replace(".apk",
                                            "-" + defaultConfig.versionName + ".apk"))
                }
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest {
                srcFile 'AndroidManifest.xml'
            }
            java {
                srcDir 'src'
            }
            res {
                srcDir 'res'
            }
            assets {
                srcDir 'assets'
            }
            resources {
                srcDir 'src'
            }
        }
    }
}


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

/*
// @from http://blog.csdn.net/look_down/article/details/17557031
task copyNativeLibs(type: Copy) {
    from fileTree(dir: 'libs', include: 'armeabi/*.so') into 'build/lib'
    from fileTree(dir: 'libs/libogg/libs', include: ['armeabi/*.so', 'armeabi-v7a/*.so', 'mips/*.so', 'x86/*.so']) into 'build/lib'
}


tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn copyNativeLibs
}


clean.dependsOn 'cleanCopyNativeLibs'
    */

tasks.withType(PackageApplication) { pkgTask ->
    pkgTask.jniFolders = [new File(buildDir, 'lib')]
}

